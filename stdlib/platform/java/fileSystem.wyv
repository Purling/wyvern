
module def fileSystem(java:Java) : {system.ffiEffect} fileSystem.FileSystem
import java:wyvern.stdlib.support.FileIO.file

/* Import Types */
import fileSystem.File
import fileSystem.Reader
import fileSystem.Writer
import fileSystem.RandomAccessFile

import wyvern.option
type Option = option.Option

effect createNewFile = {system.ffiEffect}

def makeFile(path: String) : {createNewFile} File = new
    effect read = {system.ffiEffect}
    effect write = {system.ffiEffect}
    effect open = {system.ffiEffect}
    effect close = {system.ffiEffect}
    val f : Unit = file.createNewFile(path)

    def makeReader() : {this.open} Reader
        val br = file.openBRForRead(this.f)
        new (self) =>
            effect read = {this.read}
            effect close = {this.close}
            def read() : {self.read} Int
                file.readCharFromFile(br)
            def readLine() : {self.read}  Option[String]
                val line = file.readLineFromFile(br)
                if (file.isNull(line))
                    option.None[String]()
                  else
                    option.Some[String](line)
            def readFully() : {self.read} String
                file.readFullyFile(br)
            def close() : {self.close} Unit
                file.closeReader(br)

    def makeWriter() : {this.open} Writer
        val bw = file.openBWForWrite(this.f)
        new (self) =>
            effect write = {this.write}
            effect close = {this.close}
            def write(s : String) : {self.write} Unit
                file.writeString(bw, s)        //again temporary limits on write format
            def close() : {self.close} Unit
                file.closeWriter(bw)
            
    
    /* For now this seems a little messy, considering the typical default
     * is to have a FileWriter append to a file, not overwrite...
     * Consider how to design this better
     * Name method openForAppend or openForWrite? Since they're just returning Writer
     */
    def makeAppender() : {this.open} Writer
        val bw = file.openBWForAppend(this.f)
        new (self) =>
            effect write = {this.write}
            effect close = {this.close}
            def write(s : String) : {self.write} Unit
                file.writeString(bw, s)        //again temporary limits on write format
            def close() : {self.write} Unit
                file.closeWriter(bw)
            
    
    
    /* May want to rename this to something more unique
     * Modify "mode" parameter? and available methods?
     */
    def makeRandomAccessFile(mode : String) : {this.open} RandomAccessFile
        val rf = file.makeRandomAccessFile(this.f, mode)
        new (self) =>
            effect close = {this.close}
            effect write = {this.write}
            effect read = {this.read}
            def close() : {self.close} Unit
                file.closeRandomAccessFile(rf)
            def getPosition() : {self.read} Int
                file.accessFilePointer(rf)
            def seek(pos : Int) : {self.read} Unit
                file.seekFilePointer(rf, pos)
            def length() : {self.read} Int
                file.getRandomAccessFileLength(rf)
            def readLine() : {self.read} String
                file.readLineRandomAccess(rf)
            def readUTF() : {self.read} String
                file.readUTFRandomAccess(rf)
            def write(s : String) : {self.write}  Unit
                file.writeStringRandomAccess(rf, s)            //temporary write method
            def writeUTF(s : String) : {self.write} Unit
                file.writeUTFRandomAccess(rf, s)
