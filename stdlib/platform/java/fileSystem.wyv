module def fileSystem(java:Java) : {system.ffiEffect} filesystem.FileSystem
import filesystem.fileEffects
import java:wyvern.stdlib.support.FileIO.file

/* Import Types */
import filesystem.File
import filesystem.Reader
import filesystem.Writer
import filesystem.RandomAccessFile

import wyvern.option
type Option = option.Option

def makeFile(path: String) : {system.ffiEffect} File = new
    val f : Unit = file.createNewFile(path)

    def makeReader() : {fileEffects.open} Reader
        val br = file.openBRForRead(this.f)
        new (self) =>
            def read() : {fileEffects.read} Int
                file.readCharFromFile(br)
            def readLine() : {fileEffects.read}  Option[String]
                val line = file.readLineFromFile(br)
                if (file.isNull(line))
                    option.None[String]()
                  else
                    option.Some[String](line)
            def readFully() : {fileEffects.read} String
                file.readFullyFile(br)
            def close() : {fileEffects.close} Unit
                file.closeReader(br)

    def makeWriter() : {fileEffects.open} Writer
        val bw = file.openBWForWrite(this.f)
        new (self) =>
            def write(s : String) : {fileEffects.write} Unit
                file.writeString(bw, s)        //again temporary limits on write format
            def close() : {fileEffects.close} Unit
                file.closeWriter(bw)
            
    
    /* For now this seems a little messy, considering the typical default
     * is to have a FileWriter append to a file, not overwrite...
     * Consider how to design this better
     * Name method openForAppend or openForWrite? Since they're just returning Writer
     */
    def makeAppender() : {fileEffects.open} Writer
        val bw = file.openBWForAppend(this.f)
        new (self) =>
            def write(s : String) : {fileEffects.write} Unit
                file.writeString(bw, s)        //again temporary limits on write format
            def close() : {fileEffects.write} Unit
                file.closeWriter(bw)
            
    
    
    /* May want to rename this to something more unique
     * Modify "mode" parameter? and available methods?
     */
    def makeRandomAccessFile(mode : String) : {fileEffects.open} RandomAccessFile
        val rf = file.makeRandomAccessFile(this.f, mode)
        new (self) =>
            def close() : {fileEffects.close} Unit
                file.closeRandomAccessFile(rf)
            def getPosition() : {fileEffects.read} Int
                file.accessFilePointer(rf)
            def seek(pos : Int) : {fileEffects.read} Unit
                file.seekFilePointer(rf, pos)
            def length() : {fileEffects.read} Int
                file.getRandomAccessFileLength(rf)
            def readLine() : {fileEffects.read} String
                file.readLineRandomAccess(rf)
            def readUTF() : {fileEffects.read} String
                file.readUTFRandomAccess(rf)
            def write(s : String) : {fileEffects.write}  Unit
                file.writeStringRandomAccess(rf, s)            //temporary write method
            def writeUTF(s : String) : {fileEffects.write} Unit
                file.writeUTFRandomAccess(rf, s)
