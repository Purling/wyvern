module bound

import raw
import types
import wyvern.collections.llist
type List = llist.LinkedList
type Counter = types.Counter
type Binding = types.Binding

resource type Context
    val bindings:List[Binding]
    val counter:Counter
    val parse:String -> raw.Exp
    def extend(b:Binding):Context

def Context(bs:List[Binding], c:Counter, p:String -> raw.Exp) : Context = new
    val bindings = bs
    val counter = c
    val parse = p
    def extend(b:Binding):Context
        Context(llist.Cons[Binding](b, bs), c, p)
    
def emptyContext(p:String -> raw.Exp):Context = Context(llist.Nil[Binding](), types.Counter(), p)


type Method
    val name:String
    val binding:Binding
    val body:Exp

def Method(name:String, binding:Binding, body:Exp):Method = new
    val name = name
    val binding = binding
    val body = body

    
datatype Exp
    Var(binding:Binding)
    App(func:Exp, arg:Exp)
    Lambda(binding:Binding, body:Exp)
    Obj(meths:List[Method])
    Call(receiver:Exp, name:String, arg:Exp)
    Field(receiver:Exp, field:String)
    Val(binding:Binding, exp:Exp)
    Def(binding:Binding, arg:Binding, argTyp:types.Type, retTyp:types.Type, body:Exp)
    TypeDecl(name:Binding, members:List[types.DeclType])
    New(binding:Binding, body:List[Exp])
    Seq(exps:List[Exp])
    Integer(str:String)
    UnitVal()

def lower(e:Exp):Exp = match e:
    v:Var     => v
    a:App     => Call(lower(a.func), "apply", lower(a.arg))
    l:Lambda  => UnitVal()//Obj(llist.Singleton[Method](Method("apply", l.binding, lower(l.body))))
    c:Call    => Call(lower(c.receiver),c.name,lower(c.arg))
    f:Field   => Field(lower(f.receiver),f.field)
    v:Val     => Val(v.binding, lower(v.exp))
    d:Def     => Def(d.binding, d.arg, d.argTyp, d.retTyp, lower(d.body))
    n:New     => New(n.binding, n.body.map[Exp](e => lower(e)))
    s:Seq     => Seq(s.exps.map[Exp](e => lower(e)))
    i:Integer    => i
    t:TypeDecl => UnitVal()
