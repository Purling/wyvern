import uiEffects
module def textEditor(java: Java, fs: fileSystem.FileSystem, logger: Logger, createTextEditorInstance: Unit -> TextEditor[{logger.Update, uiEffects.PaintUI, uiEffects.SetUILookAndFeel, uiEffects.ReadTextArea, uiEffects.ShowDialog, fs.Write, fs.Append}]): {system.FFI} TextEditor[{logger.Update, uiEffects.PaintUI, uiEffects.SetUILookAndFeel, uiEffects.ReadTextArea, uiEffects.ShowDialog, fs.Write, fs.Append}]

import java:wyvern.stdlib.support.TextEditorHelper.nativeJTextArea
import java:wyvern.stdlib.support.TextEditorHelper.nativeJFileChooser
import java:wyvern.stdlib.support.TextEditorHelper.nativeJScrollPane
import java:wyvern.stdlib.support.TextEditorHelper.nativeJFrame
import java:wyvern.stdlib.support.TextEditorHelper.nativeJMenuBar
import java:wyvern.stdlib.support.TextEditorHelper.nativeJMenu
import java:wyvern.stdlib.support.TextEditorHelper.nativeJOptionPane
import java:wyvern.stdlib.support.TextEditorHelper.nativeUtils
import java:wyvern.stdlib.support.TextEditorHelper.nativeActionCreator
import java:wyvern.stdlib.support.TextEditorHelper.nativeKeyListenerCreator
import java:wyvern.stdlib.support.TextEditorHelper.nativeUIManager

import OptionPane
import TextArea
import UIManager
import Plugin
import ThemePlugin

/******* Step 1: Import plugins *********************************************/

import plugins.wordCount
import plugins.darkTheme
import plugins.lightTheme
import plugins.questionnaireCreator

/****************************************************************************/


// Field initialization (most of the fields)

nativeUIManager.enableSettingLookAndFeel() // This must happen before jFrame is created.
val jFrame = nativeJFrame.create()
val textArea = nativeJTextArea.create(20, 60)
val dialog = nativeJFileChooser.create(nativeUtils.getSystemProperty("user.dir"))
var currentFile: String = "Untitled"
var changed: Boolean = false
val scroll = nativeJScrollPane.create(textArea, nativeJScrollPane.getVerticalScrollbarAlwaysValue(), nativeJScrollPane.getHorizontalScrollbarAlwaysValue())
val jmb = nativeJMenuBar.create()
val file = nativeJMenu.create("File")
val edit = nativeJMenu.create("Edit")
val plugins = nativeJMenu.create("Plugins")
val save = nativeActionCreator.create("Save")

effect Run = {logger.Update, system.FFI, uiEffects.PaintUI, uiEffects.SetUILookAndFeel, uiEffects.ReadTextArea, uiEffects.ShowDialog, fs.Write, fs.Append}

effect RegisterPlugin = {logger.Update, system.FFI}
effect RunPluginExtra = {logger.Update}

effect SaveFile = {logger.Update, system.FFI, fs.Write, fs.Append}
effect Open = {SaveFile}
effect Quit = {SaveFile}
effect PressKey = {system.FFI}


// Attenuators

def getAttenuatedNativeUIManager(): {} UIManager
  new
    effect PaintUI = {uiEffects.PaintUI}
    effect SetLookAndFeel = {uiEffects.SetUILookAndFeel}
    def paintUIAttributeBlack(element: String, attribute: String): {this.PaintUI} Unit
      nativeUIManager.paintUIAttributeBlack(element, attribute)
    def paintUIAttributeDarkGray(element: String, attribute: String): {this.PaintUI} Unit
      nativeUIManager.paintUIAttributeDarkGray(element, attribute)
    def paintUIAttributeGray(element: String, attribute: String): {this.PaintUI} Unit
      nativeUIManager.paintUIAttributeGray(element, attribute)
    def paintUIAttributeLightGray(element: String, attribute: String): {this.PaintUI} Unit
      nativeUIManager.paintUIAttributeLightGray(element, attribute)
    def paintUIAttributeWhite(element: String, attribute: String): {this.PaintUI} Unit
      nativeUIManager.paintUIAttributeWhite(element, attribute)
    def updateLookAndFeel(): {this.SetLookAndFeel} Unit
      nativeUIManager.updateLookAndFeel(jFrame)

def getAttenuatedNativeJTextArea(): {} TextArea
  new
    effect Read = {uiEffects.ReadTextArea}
    def getText(): {this.Read} String
      textArea.getText()

def getAttenuatedNativeJOptionPane(): {} OptionPane
  new
    effect ShowDialog = {uiEffects.ShowDialog}
    def showMessageDialog(message: String, title: String, messageType: Int): {this.ShowDialog} Unit
      nativeJOptionPane.showMessageDialog(message, title, messageType)
    def getPlainMessageValue(): {system.FFI} Int
      nativeJOptionPane.getPlainMessageValue()


// Plugins

/******* Step 2: Instantiate plugins passing in appropriate resources *******/

val darkTheme: Plugin[{logger.Update, uiEffects.PaintUI, uiEffects.SetUILookAndFeel}] = darkTheme(logger, getAttenuatedNativeUIManager())
val lightTheme: Plugin[{logger.Update, uiEffects.PaintUI, uiEffects.SetUILookAndFeel}] = lightTheme(logger, getAttenuatedNativeUIManager())
val wordCount: Plugin[{logger.Update, uiEffects.ReadTextArea, uiEffects.ShowDialog}] = wordCount(logger, getAttenuatedNativeJTextArea(), getAttenuatedNativeJOptionPane()) // Must pass in TextArea and not just String because need to get the latest version of the text
val questionnaireCreator: Plugin[{logger.Update, fs.Write, fs.Append, uiEffects.ReadTextArea}] = questionnaireCreator(logger, getAttenuatedNativeJTextArea(), fs) // Must pass in TextArea and not just String because need to get the latest version of the text

/****************************************************************************/


// Other methods and remaining field initialization

def registerPlugin(plugin: Plugin): {RegisterPlugin, plugin.Run} Unit
  plugins.add(nativeActionCreator.createWithAction(plugin.getName(), () => plugin.run()))
  logger.updateLog("Registered plugin " + plugin.getName() + ".\n")

def runPluginOnSetup(plugin: Plugin): {plugin.Run, RunPluginExtra} Unit
  logger.updateLog("Running plugin " + plugin.getName() + "...\n")
  plugin.run()
  logger.updateLog("Ran plugin " + plugin.getName() + ".\n")

def runThemePluginOnSetup(plugin: ThemePlugin): {plugin.Run, RunPluginExtra} Unit
  logger.updateLog("Running theme plugin " + plugin.getName() + "...\n")
  plugin.run()
  logger.updateLog("Ran theme plugin " + plugin.getName() + ".\n")

def saveFile(fileName: String): {SaveFile} Unit // N.B.: File exceptions aren't handled.
  val fileWriter = fs.fileFor(fileName).makeWriter()
  fileWriter.write(textArea.getText())
  fileWriter.close()
  logger.updateLog("File was saved as " + fileName + ".\n")
  currentFile = fileName
  jFrame.setTitle(currentFile)
  changed = false
  save.setEnabled(false)

def saveFileAs(): {SaveFile} Unit
  if (dialog.showSaveDialog(nativeUtils.getNullValue()) == nativeJFileChooser.getApproveOption())
    saveFile(dialog.getSelectedFile().getAbsolutePath())

def saveOld(): {SaveFile} Unit
  if (changed)
    if (nativeJOptionPane.showConfirmDialog("Would you like to save " + currentFile + " ?", "Save", nativeJOptionPane.getYesNoOptionValue()) == nativeJOptionPane.getYesOptionValue())
      saveFile(currentFile)

def performSaveAction(): {SaveFile} Unit
  if ("Untitled".equals(currentFile))
      saveFileAs()
    else
      saveFile(currentFile)

def performSaveAsAction(): {SaveFile} Unit
  saveFileAs()

val saveAs = nativeActionCreator.createWithAction("Save as...", () => performSaveAsAction())

def performOpenAction(): {Open} Unit // N.B.: File exceptions aren't handled.
  saveOld()
  if (dialog.showOpenDialog(nativeUtils.getNullValue()) == nativeJFileChooser.getApproveOption())
    val fileName = dialog.getSelectedFile().getAbsolutePath()
    val fileReader = fs.fileFor(fileName).makeReader()
    textArea.setText(fileReader.readFully())
    fileReader.close()
    currentFile = fileName
    jFrame.setTitle(currentFile)
    changed = false
    logger.updateLog("File " + fileName + " was opened.\n")
  saveAs.setEnabled(true)

def performQuitAction(): {Quit} Unit
  saveOld()
  logger.updateLog("Closing text editor...\n")
  nativeUtils.exitSystem()

def performNewAction(): {Run} Unit
  val te: TextEditor[{logger.Update, system.FFI, uiEffects.PaintUI, uiEffects.SetUILookAndFeel, uiEffects.ReadTextArea, uiEffects.ShowDialog, fs.Write, fs.Append}] = createTextEditorInstance()
  te.run()
  logger.updateLog("New text editor was created.\n")

val newFile = nativeActionCreator.createWithAction("New", () => performNewAction())
val open = nativeActionCreator.createWithAction("Open", () => performOpenAction())
save.setAction(() => performSaveAction())
val quit = nativeActionCreator.createWithAction("Quit", () => performQuitAction())

def performKeyPressedAction(): {PressKey} Unit
  changed = true
  save.setEnabled(true)
  saveAs.setEnabled(true)

val kl = nativeKeyListenerCreator.create(() => performKeyPressedAction())
val m = textArea.getActionMap()


// Main method

def run(): {Run} Unit
  logger.updateLog("New text-editor window is starting...\n")
  jFrame.add(scroll, "Center") // "Center" = BorderLayout.CENTER
  jFrame.setJMenuBar(jmb)

  jmb.add(file)
  jmb.add(edit)
  jmb.add(plugins)

  file.add(newFile)
  file.add(open)
  file.add(save)
  file.add(saveAs)
  file.add(quit)

  // FIX? There is a bug that if the content of the file is simply pasted from
  // the clipboard right after the file is open, the save options are
  // disabled, and so the file can't be saved unless it is manually modified.
  edit.add(m.get("cut-to-clipboard")) // "cut-to-clipboard" = DefaultEditorKit.cutAction
  edit.add(m.get("copy-to-clipboard")) // "copy-to-clipboard" = DefaultEditorKit.copyAction
  edit.add(m.get("paste-from-clipboard")) // "paste-from-clipboard" = DefaultEditorKit.pasteAction

  edit.getItem(0).setText("Cut")
  edit.getItem(1).setText("Copy")
  edit.getItem(2).setText("Paste")

  save.setEnabled(false)
  saveAs.setEnabled(false)

//******* Step 3: Register plugins with menu or run them on setup ***********/

  // Register plugins to be displayed in the menu so that the user can activate them on demand during the execution
  registerPlugin(wordCount)
  registerPlugin(questionnaireCreator)

  // Run plugins that should be run once on setup
  // Choose a theme: In Java, once the theme is changed, apparently, there is no way to change it again,
  // so only one theme can be used at a time.
  runThemePluginOnSetup(darkTheme)
  //runThemePluginOnSetup(lightTheme)

/****************************************************************************/

  jFrame.setDefaultCloseOperation(nativeJFrame.getExitOnClose())
  jFrame.pack()
  textArea.addKeyListener(kl)
  jFrame.setTitle(currentFile)
  jFrame.setVisible(true)
  logger.updateLog("New text-editor window started.\n")
